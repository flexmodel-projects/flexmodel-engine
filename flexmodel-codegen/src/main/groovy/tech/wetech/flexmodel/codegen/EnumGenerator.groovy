package tech.wetech.flexmodel.codegen

import groovy.io.GroovyPrintWriter
import groovy.util.logging.Log
import org.apache.groovy.io.StringBuilderWriter

/**
 * @author cjbi
 */
@Log
class EnumGenerator {

  File generate(EnumClass enumClass, String targetFile) {
    log.info "Generating: $targetFile"
    def file = new File(targetFile)
    new File(targetFile).withPrintWriter { out ->
      generate(out, enumClass)
    }
    return file
  }

  String generate(EnumClass enumClass) {
    def writer = new StringBuilderWriter()
    generate(new GroovyPrintWriter(writer), enumClass)
    return writer.toString()
  }

  def generate(PrintWriter out, EnumClass enumClass) {
    def className = enumClass.shortClassName
    def elements = enumClass.elements

    // Write package statement
    out.println "package ${enumClass.packageName};"
    out.println ""
    out.println "import com.fasterxml.jackson.annotation.JsonRootName;"
    out.println ""
    // Write class-level comments
    out.println "/**"
    if (enumClass.comment) {
      out.println " * ${enumClass.comment}"
    }
    out.println " * Generated by Flexmodel Generator"
    out.println " */"

    // Write class declaration
    out.println "@JsonRootName(\"${enumClass.originalEnum.name}\")"
    out.println "public enum ${className} {"
    out.print "    "
    // Write field declarations
    elements.each { element ->
      out.print "${element}, "
    }

    out.println ""
    // Close class
    out.println "}"
  }

}
