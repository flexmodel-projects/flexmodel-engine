package tech.wetech.flexmodel.codegen

import groovy.io.GroovyPrintWriter

/**
 * PojoGenerator Class
 * Generates Java POJOs based on the provided model definitions.
 *
 * Author: cjbi
 */
class PojoGenerator extends AbstractGenerator {

  /**
   * Generates a POJO class file.
   *
   * @param context Generation context containing model and target directory information.
   */
  @Override
  void generate(GenerationContext context) {
    def modelClass = context.modelClass
    System.out.println "Generating: ${modelClass.fullClassName}"

    def filePath = new File(context.targetDirectory + File.separator + "entity", "${File.separator}${modelClass.shortClassName}.java")
    filePath.withPrintWriter { out ->
      generate(out as GroovyPrintWriter, modelClass.shortClassName, context)
    }
  }

  /**
   * Writes the Java class content to the GroovyPrintWriter.
   *
   * @param out Print writer to output the Java class code.
   * @param className The name of the class.
   * @param context The generation context with model details.
   */
  def generate(GroovyPrintWriter out, String className, GenerationContext context) {
    def modelClass = context.modelClass
    def fields = modelClass.allFields

    // Write package statement
    out.println "package ${modelClass.packageName};"
    out.println ""

    // Write import statements
    if (modelClass.imports) {
      modelClass.imports.each { importStatement ->
        out.println "import ${importStatement};"
      }
      out.println ""
    }

    // Write class-level comments
    out.println "/**"
    if (modelClass.comment) {
      out.println " * ${modelClass.comment}"
    }
    out.println " * Generated by Flexmodel Generator"
    out.println " */"

    // Write class declaration
    out.println "public class ${className} {"

    // Write field declarations
    fields.each { field ->
      out.println ""
      if (field.comment) {
        out.println "  /**"
        out.println "   * ${field.comment}"
        out.println "   */"
      }
      out.println "  private ${field.shortTypeName} ${field.fieldName};"
    }

    // Write getter and setter methods
    fields.each { field ->
      generateGetter(out, field)
      generateSetter(out, field)
    }

    // Close class
    out.println "}"
  }

  /**
   * Generates a getter method for a given field.
   *
   * @param out Print writer to output the getter method.
   * @param field Field details for which the getter is generated.
   */
  def generateGetter(GroovyPrintWriter out, field) {
    out.println ""
    out.println "  public ${field.shortTypeName} get${field.fieldName.capitalize()}() {"
    out.println "    return ${field.fieldName};"
    out.println "  }"
  }

  /**
   * Generates a setter method for a given field.
   *
   * @param out Print writer to output the setter method.
   * @param field Field details for which the setter is generated.
   */
  def generateSetter(GroovyPrintWriter out, field) {
    out.println ""
    out.println "  public void set${field.fieldName.capitalize()}(${field.shortTypeName} ${field.fieldName}) {"
    out.println "    this.${field.fieldName} = ${field.fieldName};"
    out.println "  }"
  }

}
